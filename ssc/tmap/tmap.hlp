{smcl}
{* 4jan2005}{...}
{hline}
help for {hi:tmap (v2.0b)}
{hline}

{title:Simple thematic mapping}

{p 8 14} {cmd:tmap} {cmdab:cho:ropleth} {it:quantvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:i:d}{cmd:(}{it:varname}{cmd:)}
{cmdab:m:ap}{cmd:(}{it:filename}{cmd:)}
[
{cmdab:clm:ethod}{cmd:(quantile} | {cmd:eqint} | {cmd:stdev} | {cmd:custom} | {cmd:unique)}
{cmdab:cln:umber}{cmd:(}{it:#}{cmd:)}
{cmdab:clb:reaks}{cmd:(}{it:numlist}{cmd:)}
{cmdab:eir:ange}{cmd:(}{it:numlist}{cmd:)}
{cmdab:p:alette}{cmd:(}{it:colorscheme}{cmd:)}
{cmdab:c:olors}{cmd:(}{it:colorstyle_list}{cmd:)}
{cmdab:oc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:os:ize}{cmd:(}{it:linewidthstyle}{cmd:)}
{cmdab:bc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:tit:le}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:sub:title}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:not:e}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:cap:tion}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:legp:os}{cmd:(}{it:#}{cmd:)}
{cmdab:legcol:or}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:legs:ize}{cmd:(}{it:#}{cmd:)}
{cmdab:legf:ormat}{cmd:(}{it:format}{cmd:)}
{cmdab:legt:itle}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:legb:ox}{cmd:(}{it:roptions}{cmd:)}
{cmdab:legc:ount}
{cmdab:noleg:end}
{cmdab:add:plot}{cmd:(}{it:command}{cmd:)}
]

{p 8 14} {cmd:tmap} {cmdab:pro:psymbol} {it:quantvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:m:ap}{cmd:(}{it:filename}{cmd:)}
[
{cmdab:sc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:ssh:ape}{cmd:(}{it:symbolstyle}{cmd:)}
{cmdab:ssi:ze}{cmd:(}{it:#}{cmd:)}
{cmdab:so:utline}
{cmdab:oc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:os:ize}{cmd:(}{it:linewidthstyle}{cmd:)}
{cmdab:fc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:bc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:tit:le}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:sub:title}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:not:e}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:cap:tion}{cmd:(}{it:tinfo}{cmd:)}
]

{p 8 14} {cmd:tmap} {cmdab:dev:iation} {it:quantvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:m:ap}{cmd:(}{it:filename}{cmd:)}
[
{cmdab:c:enter}{cmd:(mean} | {cmd:median)}
{cmdab:sc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:ssh:ape}{cmd:(}{it:symbolstyle}{cmd:)}
{cmdab:ssi:ze}{cmd:(}{it:#}{cmd:)}
{cmdab:oc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:os:ize}{cmd:(}{it:linewidthstyle}{cmd:)}
{cmdab:fc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:bc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:tit:le}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:sub:title}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:not:e}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:cap:tion}{cmd:(}{it:tinfo}{cmd:)}
]

{p 8 14} {cmd:tmap dot}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:m:ap}{cmd:(}{it:filename}{cmd:)}
[
{cmdab:by}{cmd:(}{it:varname}{cmd:)}
{cmdab:mar:ker}{cmd:(color} | {cmd:shape} | {cmd:both)}
{cmdab:sc:olor}{cmd:(}{it:colorstyle_list}{cmd:)}
{cmdab:ssh:ape}{cmd:(}{it:symbolstyle_list}{cmd:)}
{cmdab:ssi:ze}{cmd:(}{it:#}{cmd:)}
{cmdab:so:utline}
{cmdab:oc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:os:ize}{cmd:(}{it:linewidthstyle}{cmd:)}
{cmdab:fc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:bc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:tit:le}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:sub:title}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:not:e}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:cap:tion}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:legp:os}{cmd:(}{it:#}{cmd:)}
{cmdab:legcol:or}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:legs:ize}{cmd:(}{it:#}{cmd:)}
{cmdab:legt:itle}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:legb:ox}{cmd:(}{it:roptions}{cmd:)}
{cmdab:legc:ount}
{cmdab:noleg:end}
]

{p 8 14} {cmd:tmap} {cmdab:lab:el} {it:labvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:m:ap}{cmd:(}{it:filename}{cmd:)}
[
{cmdab:lc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:ls:ize}{cmd:(}{it:#}{cmd:)}
{cmdab:ll:ength}{cmd:(}{it:#}{cmd:)}
{cmdab:oc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:os:ize}{cmd:(}{it:linewidthstyle}{cmd:)}
{cmdab:fc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:bc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:tit:le}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:sub:title}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:not:e}{cmd:(}{it:tinfo}{cmd:)}
{cmdab:cap:tion}{cmd:(}{it:tinfo}{cmd:)}
]


{title:Description}

{p 4 4} {cmd:map choropleth} represents the spatial distribution of area data
by means of choropleth maps, i.e., maps where each of {it:n} sub-areas of the
geographical unit of interest {it:R} is colored (or shaded) according to a
discrete scale based on the value taken on by the variable of
interest {it:quantvar} in that sub-area. Optionally, a propsymbol, deviation,
dot, or label plot can be superimposed onto the current choropleth map.

{p 4 4} {cmd:map propsymbol} represents the spatial distribution of area data
by means of proportional symbol maps, i.e., maps where the value taken on
by the variable of interest {it:quantvar} in each of {it:n} sub-areas of the
geographical unit of interest {it:R} is represented by a symbol whose
size is proportional to the value itself.
 
{p 4 4} {cmd:map deviation} represents the spatial distribution of area data
by means of deviation maps, a particular kind of proportional symbol maps. In
the first place, the values taken on by the variable of interest {it:quantvar}
are re-expressed as deviations from the "center" of their distribution,
defined as the mean or the median of {it:quantvar}. The value taken on by the
transformed variable in each of {it:n} sub-areas of the geographical
unit of interest {it:R} is then represented by a symbol whose size is
proportional to the value itself expressed in absolute terms, and whose
filling reflects the sign of the value: positive values are represented by
solid symbols, while negative values are represented by hollow symbols of the
same shape.

{p 4 4} {cmd:map dot} represents the spatial distribution of point data
by means of dot maps, i.e., maps where the {it:n} locations at which some
"events" of interest have occurred are indicated by symbols whose color and/or
shape can vary according to the type of event.

{p 4 4} {cmd:map label} is an auxiliary program that allows the user to superimpose
onto a base map the values taken on by a numeric or string variable {it:labvar} at different
locations. This program can be used, for example, to plot sub-area names or to
represent the spatial distribution of a given variable of interest in numeric
form.


{title:Common options}

{p 4 8} {cmd:map(}{it:filename}{cmd:)} is required. It specifies the name of the
file containing the information needed to draw the base map. In {cmd:tmap choropleth},
{it:filename} must contain the coordinates of the polygons representing the different
sub-areas of the geographical unit of interest {it:R}. In all the other cases,
{it:filename} can contain either the coordinates of the polygons representing the
different sub-areas, or the coordinates of the polygons representing the whole
geographical unit of interest {it:R}.

{p 4 8} {cmd:ocolor(}{it:{help colorstyle}}{cmd:)} specifies the outline color of the
polygons making up the base map. The default is {cmd:ocolor(black)}.

{p 4 8} {cmd:osize(}{it:{help linewidthstyle}}{cmd:)} specifies the outline thickness of the
polygons making up the base map. The default is {cmd:osize(thin)}.

{p 4 8} {cmd:fcolor(}{it:{help colorstyle}}{cmd:)} specifies the fill color of the
polygons making up the base map. The default is {cmd:fcolor(white)}.

{p 4 8} {cmd:bcolor(}{it:{help colorstyle}}{cmd:)} specifies the background color of the
graph. The default is {cmd:bcolor(white)}.

{p 4 8} {cmd:title(}{help title_options:{it:tinfo}}{cmd:)} specifies the overall title of the graph.

{p 4 8} {cmd:subtitle(}{help title_options:{it:tinfo}}{cmd:)} specifies the subtitle of the graph.

{p 4 8} {cmd:note(}{help title_options:{it:tinfo}}{cmd:)} specifies notes to be displayed with the graph.

{p 4 8} {cmd:caption(}{help title_options:{it:tinfo}}{cmd:)} specifies an explanation to accompany the graph.


{title:Options for tmap choropleth}

{p 4 8} {cmd:id(}{it:varname}{cmd:)} is required. It specifies the name of
the numeric variable that uniquely identifies the different sub-areas of
the geographical unit of interest {it:R}. The values taken on by {it:varname}
must correspond to the values taken on by the identifier _ID contained in the
file specified with option {cmd:map(}{it:filename}{cmd:)}.

{p 4 8} {cmd:clmethod(quantile} | {cmd:eqint} | {cmd:stdev} | {cmd:custom} | {cmd:unique) }
specifies the method to be used for determining the class breaks.

{p 8 8} {cmd:clmethod(quantile)} is the default and requests that the quantiles
method be used.

{p 8 8} {cmd:clmethod(eqint)} requests that the equal intervals
method be used.

{p 8 8} {cmd:clmethod(stdev)} requests that the standard deviates
method be used.

{p 8 8} {cmd:clmethod(custom)} requests that class breaks be specified by the user
with option {cmd:clbreaks(}{it:numlist}{cmd:)}.

{p 8 8} {cmd:clmethod(unique)} requests that the variable of interest {it:quantvar} be
treated as a categorical variable taking on a maximum of nine different values.

{p 4 8} {cmd:clnumber(}{it:#}{cmd:)} specifies the number of classes {it:k} in
which the variable of interest {it:quantvar} should be divided. This option accepts
only numbers between 2 and 9. The default is {cmd:clnumber(4)}.

{p 4 8} {cmd:clbreaks(}{it:{help numlist}}{cmd:)} is required if option
{cmd:clmethod(custom)} is specified. It specifies a list of numbers defined as
follows: the first element of the list is the minimum value of {it:quantvar} to be
considered; the second to {it:k}th elements of the list are the class breaks;
the last element of the list is the maximum value of {it:quantvar} to be considered.

{p 4 8} {cmd:eirange(}{it:{help numlist}}{cmd:)} specifies the range of values
(minimum and maximum) to be considered in the calculation of class breaks when
option {cmd:clmethod(eqint)} is specified. This option overrides the default
range [min({it:quantvar}),max({it:quantvar})].

{p 4 8} {cmd:palette(}{it:colorscheme}{cmd:)} specifies the color scheme to be used
for representing the different classes in which {it:quantvar} has been divided. {it:colorscheme}
is one of the following:{p_end}
		Blues		BrBG		Greens		Greys
		Paired		PuRd		Purples		RdBu
		RdGy		Reds		Set1		Set3
		YlOrBr		Custom
{p 8 8} The default is {cmd:palette(Greys)} when {cmd:clmethod(quantile)} or {cmd:clmethod(eqint)}
is specified; {cmd:palette(RdBu)} when {cmd:clmethod(stdev)} is specified; and {cmd:palette(Paired)}
when {cmd:clmethod(unique)} is specified. If option {cmd:palette(Custom)} is specified, option
{cmd:colors(}{it:colorstyle_list}{cmd:)} must be specified as well.

{p 4 8} {cmd:colors(}{help colorstyle:{it:colorstyle_list}}{cmd:)} specifies a custom
list of colors to be used for representing the different classes in which {it:quantvar}
has been divided. The number of elements of the list must equal {it:k}, i.e., the
desired number of classes.

{p 4 8} {cmd:legpos(}{it:{help clockpos}}{cmd:)} specifies the position of the map legend. The
default is {cmd:legpos(7)}.

{p 4 8} {cmd:legcolor(}{it:{help colorstyle}}{cmd:)} specifies the color of the main text
of the map legend. The default is {cmd:legcolor(black)}.

{p 4 8} {cmd:legsize(}{it:#}{cmd:)} specifies a multiplier that affects the size of the
main text of the map legend. For example, to increase the default size of the text by 50%,
specify {cmd:legsize(1.5)}. The default is {cmd:legsize(1)}.

{p 4 8} {cmd:legformat(}{it:{help format}}{cmd:)} specifies the format of the numeric
values appearing in the map legend. The default is {cmd:legformat(%8.2f)}.

{p 4 8} {cmd:legtitle(}{help title_options:{it:tinfo}}{cmd:)} specifies the title of
the map legend. By default, no title is used.

{p 4 8} {cmd:legbox(}{help legend_option:{it:roptions}}{cmd:)} requests that a box be
drawn around the map legend and specifies its appearance.

{p 4 8} {cmd:legcount} requests that the number of sub-areas belonging to each class {it:k}
in which {it:quantvar} has been divided be displayed in the map legend.

{p 4 8} {cmd:nolegend} requests that the map legend be suppressed.

{p 4 8} {cmd:addplot(}{it:command}{cmd:)} requests that a propsymbol, deviation, dot or
label plot be superimposed onto the current choropleth map. {it:command} is one of the
following:{p_end}

{p 8 14} {cmd:propsymbol} {it:quantvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
[
{cmdab:sc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:ssh:ape}{cmd:(}{it:symbolstyle}{cmd:)}
{cmdab:ssi:ze}{cmd:(}{it:#}{cmd:)}
{cmdab:so:utline}
]

{p 8 14} {cmd:deviation} {it:quantvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
[
{cmdab:c:enter}{cmd:(mean} | {cmd:median)}
{cmdab:sc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:ssh:ape}{cmd:(}{it:symbolstyle}{cmd:)}
{cmdab:ssi:ze}{cmd:(}{it:#}{cmd:)}
]

{p 8 14} {cmd:dot}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
[
{cmdab:by}{cmd:(}{it:varname}{cmd:)}
{cmdab:mar:ker}{cmd:(color} | {cmd:shape} | {cmd:both)}
{cmdab:sc:olor}{cmd:(}{it:colorstyle_list}{cmd:)}
{cmdab:ssh:ape}{cmd:(}{it:symbolstyle_list}{cmd:)}
{cmdab:ssi:ze}{cmd:(}{it:#}{cmd:)}
{cmdab:so:utline}
]

{p 8 14} {cmd:label} {it:labvar}
[{cmd:if} {it:exp}] [{cmd:in} {it:range}] {cmd:,}
{cmdab:x:coord}{cmd:(}{it:varname}{cmd:)}
{cmdab:y:coord}{cmd:(}{it:varname}{cmd:)}
[
{cmdab:lc:olor}{cmd:(}{it:colorstyle}{cmd:)}
{cmdab:ls:ize}{cmd:(}{it:#}{cmd:)}
{cmdab:ll:ength}{cmd:(}{it:#}{cmd:)}
]


{title:Options for tmap propsymbol}

{p 4 8} {cmd:xcoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:x}-coordinate of the centroid of each sub-area
of the geographical unit of interest {it:R}.

{p 4 8} {cmd:ycoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:y}-coordinate of the centroid of each sub-area
of the geographical unit of interest {it:R}.

{p 4 8} {cmd:scolor(}{it:{help colorstyle}}{cmd:)} specifies the color of the
symbols. The default is {cmd:scolor(black)}.

{p 4 8} {cmd:sshape(}{it:{help symbolstyle}}{cmd:)} specifies the shape of the
symbols. The default is {cmd:sshape(Oh)}.

{p 4 8} {cmd:ssize(}{it:#}{cmd:)} specifies a multiplier that affects the size
of the symbols. For example, to increase the size of all the symbols by 50%,
specify {cmd:ssize(1.5)}. The default is {cmd:ssize(1)}.

{p 4 8} {cmd:soutline} requests that the symbols be drawn with a black outline.


{title:Options for tmap deviation}

{p 4 8} {cmd:xcoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:x}-coordinate of the centroid of each sub-area
of the geographical unit of interest {it:R}.

{p 4 8} {cmd:ycoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:y}-coordinate of the centroid of each sub-area
of the geographical unit of interest {it:R}.

{p 4 8} {cmd:center(mean} | {cmd:median) }
specifies the center of the distribution of {it:quantvar} to be taken as the
reference value.

{p 8 8} {cmd:center(mean)} is the default requesting that the reference value
be the arithmetic mean of {it:quantvar}.

{p 8 8} {cmd:center(median)} requests that the reference value be the median
of {it:quantvar}.

{p 4 8} {cmd:scolor(}{it:{help colorstyle}}{cmd:)} specifies the color of the
symbols. The default is {cmd:scolor(black)}.

{p 4 8} {cmd:sshape(}{it:{help symbolstyle}}{cmd:)} specifies the shape of the
symbols. This option accepts only solid symbolstyles expressed in short form,
namely O D T S o d t s. The default is {cmd:sshape(O)}.

{p 4 8} {cmd:ssize(}{it:#}{cmd:)} specifies a multiplier that affects the size
of the symbols. For example, to increase the size of all the symbols by 50%,
specify {cmd:ssize(1.5)}. The default is {cmd:ssize(1)}.


{title:Options for tmap dot}

{p 4 8} {cmd:xcoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:x}-coordinate of the locations at which the
events of interest have occurred.

{p 4 8} {cmd:ycoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:y}-coordinate of the locations at which the
events of interest have occurred.

{p 4 8} {cmd:by(}{it:varname}{cmd:)} specifies the name of a categorical variable
denoting the type of event that occurred at each location. Although the program
does not impose any restriction, it is advisable that {it:varname} take a maximum
of nine different values.

{p 4 8} {cmd:marker(color} | {cmd:shape} | {cmd:both)}
when {cmd:by(}{it:varname}{cmd:)} is specified, specifies whether the
different types of event should be indicated by symbols having the same shape
but different colors, by symbols having the same color but different shapes,
or by symbols having both different colors and different shapes.

{p 8 8} {cmd:marker(color)} is the default and requests that the different types
of event be indicated by symbols having the same shape but different colors.

{p 8 8} {cmd:marker(shape)} requests that the different types of event be indicated
by symbols having the same color but different shapes.

{p 8 8} {cmd:marker(both)} requests that the different types of event be indicated
by symbols having both different colors and different shapes.

{p 4 8} {cmd:scolor(}{help colorstyle:{it:colorstyle_list}}{cmd:)} specifies the
colors of the symbols. When {cmd:by(}{it:varname}{cmd:)} is not specified or is
specified along with {cmd:marker(shape)}, the default is {cmd:scolor(black)}. When
{cmd:by(}{it:varname}{cmd:)} is specified along with {cmd:marker(color)} or
{cmd:marker(both)}, the default is {cmd:scolor(black red blue green orange ltblue lime sienna yellow)}.

{p 4 8} {cmd:sshape(}{help symbolstyle:{it:symbolstyle_list}}{cmd:)} specifies the
shapes of the symbols. When {cmd:by(}{it:varname}{cmd:)} is not specified or is
specified along with {cmd:marker(color)}, the default is {cmd:sshape(o)}. When
{cmd:by(}{it:varname}{cmd:)} is specified along with {cmd:marker(shape)} or
{cmd:marker(both)}, the default is {cmd:sshape(o oh s sh t th d dh x)}.

{p 4 8} {cmd:ssize(}{it:#}{cmd:)} specifies a multiplier that affects the size
of the symbols. For example, to increase the size of all the symbols by 50%,
specify {cmd:ssize(1.5)}. The default is {cmd:ssize(1)}.

{p 4 8} {cmd:soutline} requests that the symbols be drawn with a black outline.

{p 4 8} {cmd:legpos(}{it:{help clockpos}}{cmd:)} specifies the position of the map legend. The
default is {cmd:legpos(7)}.

{p 4 8} {cmd:legcolor(}{it:{help colorstyle}}{cmd:)} specifies the color of the main text
of the map legend. The default is {cmd:legcolor(black)}.

{p 4 8} {cmd:legsize(}{it:#}{cmd:)} specifies a multiplier that affects the size of the
main text of the map legend. For example, to increase the default size of the text by 50%,
specify {cmd:legsize(1.5)}. The default is {cmd:legsize(1)}.

{p 4 8} {cmd:legtitle(}{help title_options:{it:tinfo}}{cmd:)} specifies the title of
the map legend. By default, no title is used.

{p 4 8} {cmd:legbox(}{help legend_option:{it:roptions}}{cmd:)} requests that a box be
drawn around the map legend and specifies its appearance.

{p 4 8} {cmd:legcount} requests that the number of locations belonging to each possible
type of event be displayed in the map legend.

{p 4 8} {cmd:nolegend} requests that the map legend be suppressed.


{title:Options for tmap label}

{p 4 8} {cmd:xcoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:x}-coordinate of the locations at which the
labels of interest should be plotted.

{p 4 8} {cmd:ycoord(}{it:varname}{cmd:)} is required. It specifies the name of
the variable containing the {it:y}-coordinate of the locations at which the
labels of interest should be plotted.

{p 4 8} {cmd:lcolor(}{it:{help colorstyle}}{cmd:)} specifies the color of the
labels. The default is {cmd:lcolor(black)}.

{p 4 8} {cmd:lsize(}{it:#}{cmd:)} specifies a multiplier that affects the size
of the labels. For example, to increase the size of all the labels by 50%,
specify {cmd:lsize(1.5)}. The default is {cmd:lsize(1)}.

{p 4 8} {cmd:llength(}{it:#}{cmd:)} specifies the maximum number of characters
of the labels to be displayed. The default is {cmd:llength(12)}.


{title:Examples}

{p 4 8} {inp:. use Us-Database.dta, clear}{p_end}
{p 4 8} {inp:. tmap choropleth murder, id(id) map(Us-Coordinates.dta)}{p_end}
{p 4 8} {inp:. tmap cho murder if conterminous, id(id) map(Us-Coordinates.dta)}{p_end}
{p 4 8} {inp:. tmap cho murder if conterminous, id(id) map(Us-Coordinates.dta) palette(Blues) ocolor(white) title(`"`"Murders per 100,000 population"'"') subtitle("United States 1994")}{p_end}
{p 4 8} {inp:. tmap propsymbol murder if conterminous, x(x_coord) y(y_coord) map(Us48-Coordinates.dta) sshape(o) scolor(edkblue) fcolor(eltblue) title(`"`"Murders per 100,000 population"'"') subtitle("United States 1994")}{p_end}
{p 4 8} {inp:. tmap deviation murder if conterminous, x(x_coord) y(y_coord) map(Us48-Coordinates.dta) sshape(s) scolor(sienna) fcolor(eggshell) title(`"`"Murders per 100,000 population"'"') subtitle("United States 1994")}{p_end}
{p 4 8} {inp:. tmap label label if conterminous, x(x) y(y) map(Us48-Coordinates.dta) lc(white) ls(0.9) fc(emerald)}{p_end}

{p 4 8} {inp:. use MilanoPolice-Database.dta, clear}{p_end}
{p 4 8} {inp:. tmap dot, x(x) y(y) map(MilanoOutline-Coordinates.dta) by(type) marker(both) sshape(s d) title("Location of police stations") subtitle("Milano 2004") legtitle("Police force", size(*0.7)) legbox(lc(black))}{p_end}


{title:Author}

{p 4} Maurizio Pisati {p_end}
{p 4} Department of Sociology and Social Research {p_end}
{p 4} University of Milano Bicocca - Italy {p_end}
{p 4} maurizio.pisati@unimib.it {p_end}


{title:Aknowledgments}

{p 4 4} The color schemes used in {cmd:tmap choropleth} were designed by Dr. Cynthia
A. Brewer, Department of Geography, The Pennsylvania State University, University
Park, Pennsylvania, USA. The color schemes are used with Dr. Brewer's permission
and are from the ColorBrewer map design tool available at {browse "http://www.ColorBrewer.org"}. I
wish to thank an anonymous reviewer, Nick Cox and Vince Wiggins for their valuable suggestions
and for helping improve the {cmd:tmap} package. Any remaining errors are mine.



